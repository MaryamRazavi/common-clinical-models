archetype (adl_version=1.4)
	openEHR-EHR-OBSERVATION.respiration.v1

concept
	[at0000]	-- Respiration
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["sv"] = <
			language = <[ISO_639-1::sv]>
			author = <
				["name"] = <"Oskar Nielsen">
				["organisation"] = <"Cambio Healthcare Systems">
			>
		>
	>
description
	original_author = <
		["name"] = <"Ian McNicoll">
		["email"] = <"ian.mcnicoll@oceaninformatics.com">
		["organisation"] = <"Ocean Informatics, United Kingdom">
		["date"] = <"2009-07-17">
	>
	details = <
		["sv"] = <
			language = <[ISO_639-1::sv]>
			purpose = <"Att registrera de observerade egenskaperna för respiration hos individer.">
			use = <"Används för att regsistrera de observerade och uppmätta egenskaperna relaterade till spontana andetag hos en person, inklusive andningsfrekvens, djup och rytm.

Andetag registreras vanligen som en del av vitalparametrar - bestående av blodtryck, puls, temperatur, och saturation. Det finns ytterligare specifika arketyper för varje begrepp.

Andetag bör mätas före matintag bland nyfödda och spädbarn.">
			keywords = <"andning", "andetag", "frekvens", "af", "respiration">
			misuse = <"Får ej användas för att registrera andra aspekter av en bredare undersökning eller bedömning för andning. Andra specifika arketyper ska användas för att spela in egenskaper såsom andningsansträngning, auskultatoriska resultat etc.

Får ej användas för att registrera uppgifter om när personen genomgår assisterad ventilation.">
			copyright = <"openEHR Foundation">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the observed characteristics of spontaneous breathing.">
			use = <"Use to record the observed and measured characteristics related to spontaneous respirations in a person, including respiratory rate, depth and rhythm.

Respirations are commonly recorded as one component of Vital signs - comprising Blood Pressure, Pulse, Temperature, and Oximetry.  There are additional specific archetypes for each of these concepts.

Respirations should be measured before feeds in neonates and young infants.">
			keywords = <"respirations", "breathing", "breath", "resps", "respiration">
			misuse = <"Not to be used to try to record other aspects of the broader respiratory examination or assessment.  Other specific archetypes will be used to record characteristics such as respiratory effort, auscultatory findings etc.

Not to be used to record details  when the subject is undergoing assisted ventilation.">
			copyright = <"? openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Marja Buur, Medisch Centrum Alkmaar, Netherlands", "Gregory Caulton, PatientOS Inc., United States", "Stephen Chu, NeHTA, Australia", "Sebastian Garde, Ocean Informatics, Germany", "Son Ghee Yap, Singapore Health Services, Singapore", "Anne Harbison, CPCER, Australia", "Sam Heard, Ocean Informatics, Australia", "Omer Hotomargolu, Turkey", "Sundaresan Jagannathan, Scottish NHS, United Kingdom", "Andrew James, University of Toronto, Canada", "Heather Leslie, Ocean Informatics, Australia", "Rikard Lovstrom, Swedish Medical Association, Sweden", "Jeroen Meintjens, Medisch Centrum Alkmaar, Netherlands", "Soon Ghee Yap, Singapore Health Services Pte Ltd, Singapore">
	other_details = <
		["references"] = <"Braun RB.  Respiratory Rate and Pattern. In: Walker HK, Hall WD, Hurst JW. Clinical methods: the history, physical, and laboratory examinations. [Internet] 3rd ed. Stoneham (MA): Butterworth Publishers; c1990 [cited 2009 Jul 21] 
Available from: http://www.ncbi.nlm.nih.gov/bookshelf/br.fcgi?book=cm&part=A1308 [Accessed July 22, 2009].

Gaunt AC, Frang T. Examination of the Respiratory System. The Journal of Clinical Examination 2007 (4): 14-22 
Available from:   http://www.thejce.com/downloads/journals/oct_2007/14-22%20Gaunt%20and%20Frang%20-%20Respiratory%20System.pdf  [Accessed July 22, 2009]

Cheyne-Stokes respiration - Wikipedia, the free encyclopedia [Internet]. [cited 2009 Jul 25 ] 
Available from: http://en.wikipedia.org/wiki/Cheyne-Stokes_respiration

Biot's respiration - Wikipedia, the free encyclopedia [Internet]. [cited 2009 Jul 25 ] 
Available from: http://en.wikipedia.org/wiki/Biot%27s_respiration

Apneustic respirations - Wikipedia, the free encyclopedia [Internet]. [cited 2009 Jul 25 ] 
Available from: http://en.wikipedia.org/wiki/Apneustic_respirations
">
		["MD5-CAM-1.0.1"] = <"615A8D753CD964A5BDEEC967012A7512">
	>

definition
	OBSERVATION[at0000] matches {	-- Respiration
		data matches {
			HISTORY[at0001] matches {	-- Historia
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {	-- Vilken händelse som helst
						data matches {
							ITEM_TREE[at0003] matches {	-- Lista
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {	-- Andningsfrekvens
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::382]>
												list = <
													["1"] = <
														units = <"breaths/min">
														magnitude = <|0.0..200.0|>
														precision = <|0|>
													>
												>
											>
										}
									}
									ELEMENT[at0005] occurrences matches {0..1} matches {	-- Rytm
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0006, 	-- Regelbunden
													at0007]	-- Oregelbunden
												}
											}
										}
									}
									ELEMENT[at0016] occurrences matches {0..1} matches {	-- Djup
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0017, 	-- Normal
													at0018, 	-- Ytlig
													at0019, 	-- Djup
													at0025]	-- Variabel
												}
											}
										}
									}
									ELEMENT[at0024] occurrences matches {0..1} matches {	-- Beskrivning
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0009] occurrences matches {0..1} matches {	-- Onormalt andningsmönster
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0012, 	-- Kussmauls andning
													at0030, 	-- Cheyne-Stokes andning
													at0031, 	-- Ataktisk andning
													at0033, 	-- Apneustisk andning
													at0034, 	-- Biots andning
													at0044, 	-- Apné
													at0054]	-- Långvarig utandningsfas
												}
											}
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
						state matches {
							ITEM_TREE[at0022] matches {	-- Lista
								items cardinality matches {0..*; unordered} matches {
									allow_archetype CLUSTER[at0055] occurrences matches {0..1} matches {	-- Ambient syre
										include
											archetype_id/value matches {/openEHR-EHR-CLUSTER\.ambient_oxygen(-[a-zA-Z0-9_]+)*\.v1/}
									}
									ELEMENT[at0056] occurrences matches {0..*} matches {	-- Andra påverkande faktorer
										value matches {
											DV_TEXT matches {*}
										}
									}
									allow_archetype CLUSTER[at0037] occurrences matches {0..1} matches {	-- Ansträngning
										include
											archetype_id/value matches {/openEHR-EHR-CLUSTER\.level_of_exertion(-[a-zA-Z0-9_]+)*\.v1/}
									}
								}
							}
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Respirations">
					description = <"The observed characteristics of spontaneous breathing as would commonly be recorded as part of a 'vital signs' examination.">
				>
				["at0001"] = <
					text = <"history">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Generic event">
				>
				["at0003"] = <
					text = <"List">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Respiration rate">
					description = <"Rate of respiration.">
				>
				["at0005"] = <
					text = <"Rhythm">
					description = <"Rhythm of respiration.">
				>
				["at0006"] = <
					text = <"Regular">
					description = <"Regular respiration.">
				>
				["at0007"] = <
					text = <"Irregular">
					description = <"Irregular respirations.">
				>
				["at0009"] = <
					text = <"Abnormal respiratory pattern">
					description = <"Specific abnormal respiratory pattern. Other patterns may be entered as free text or terminology subset defined at specialisation or template level.">
				>
				["at0012"] = <
					text = <"Kussmaul's respiration">
					description = <"Deep chest breathing with or without a visible gasp.">
				>
				["at0016"] = <
					text = <"Depth">
					description = <"Depth of respiration.">
				>
				["at0017"] = <
					text = <"Normal">
					description = <"Normal depth of breathing.">
				>
				["at0018"] = <
					text = <"Shallow">
					description = <"Shallow depth of breathing.">
				>
				["at0019"] = <
					text = <"Deep">
					description = <"Deep breathing.">
				>
				["at0022"] = <
					text = <"List">
					description = <"@ internal @">
				>
				["at0024"] = <
					text = <"Description">
					description = <"A text description of respirations.">
				>
				["at0025"] = <
					text = <"Variable">
					description = <"Variable depth of breathing.">
				>
				["at0030"] = <
					text = <"Cheyne-Stokes respiration">
					description = <"Periods of hyperventilation alternating with periods of apnoea.">
				>
				["at0031"] = <
					text = <"Ataxic respiration">
					description = <"Breathing of varying tidal volumes and rates.">
				>
				["at0033"] = <
					text = <"Apneustic respiration">
					description = <"Deep, gasping inspiration with a pause at full inspiration followed by a brief, insufficient release of breath.">
				>
				["at0034"] = <
					text = <"Cluster breathing">
					description = <"Clusters of irregular breaths that alternate with periods of apnoea. Also termed Biot's breathing.">
				>
				["at0037"] = <
					text = <"Exertion">
					description = <"Subject's level of exertion at or just prior to the observation being made. Intended only to record exertion only as it might effect respirations and where it would not normally be recorded as part of general clinical observation.">
				>
				["at0044"] = <
					text = <"Apnoea">
					description = <"Breathing has ceased.">
				>
				["at0054"] = <
					text = <"Prolonged expiratory phase">
					description = <"The respiratory expiratory phase is longer than normal/usual. Associated with obstructive airways disease such as asthma.">
				>
				["at0055"] = <
					text = <"Ambient oxygen">
					description = <"Details of the amount of oxygen being delivered to the subject at the time of observation.  Assumed values of 21% oxygen concentration, Fi02 of 0.21 and oxygen flow rate of 0 l/min or 0 ml/min.">
				>
				["at0056"] = <
					text = <"Confounding factors">
					description = <"Comment on and record other incidental factors that may be contributing to the respirations observation. For example, level of anxiety, pain, feeding in infants, tracheostomy.">
				>
			>
		>
		["sv"] = <
			items = <
				["at0000"] = <
					text = <"Respiration">
					description = <"Observerade egenskaper för spontan andning som vanligen skulle registreras som en del av en undersökning av vitalparametrar.">
				>
				["at0001"] = <
					text = <"Historia">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Vilken händelse som helst">
					description = <"Generisk händelse">
				>
				["at0003"] = <
					text = <"Lista">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Andningsfrekvens">
					description = <"Takten i vilken personen andas.">
				>
				["at0005"] = <
					text = <"Rytm">
					description = <"Rytmen för andningen.">
				>
				["at0006"] = <
					text = <"Regelbunden">
					description = <"Regelbunden respiration.">
				>
				["at0007"] = <
					text = <"Oregelbunden">
					description = <"Oregelbunden respiration.">
				>
				["at0009"] = <
					text = <"Onormalt andningsmönster">
					description = <"Specifik onormalt andningsmönster. Andra mönster kan skrivas in som fri text eller terminologidel som definieras på specialisering- eller templatenivå.">
				>
				["at0012"] = <
					text = <"Kussmauls andning">
					description = <"Djup andning med eller utan en synlig flämtning.">
				>
				["at0016"] = <
					text = <"Djup">
					description = <"Andningens djup.">
				>
				["at0017"] = <
					text = <"Normal">
					description = <"Normalt djup andning.">
				>
				["at0018"] = <
					text = <"Ytlig">
					description = <"Ytlig andning.">
				>
				["at0019"] = <
					text = <"Djup">
					description = <"Djup andning.">
				>
				["at0022"] = <
					text = <"Lista">
					description = <"@ internal @">
				>
				["at0024"] = <
					text = <"Beskrivning">
					description = <"En textbeskrivning av andetag.">
				>
				["at0025"] = <
					text = <"Variabel">
					description = <"Olika andningsdjup.">
				>
				["at0030"] = <
					text = <"Cheyne-Stokes andning">
					description = <"Perioder av hyperventilation omväxlande med perioder av apné.">
				>
				["at0031"] = <
					text = <"Ataktisk andning">
					description = <"Inandning i varierande tendenser för volym och frekvens.">
				>
				["at0033"] = <
					text = <"Apneustisk andning">
					description = <"Djup, kippande inandning med en paus vid full inandning följt av en kort och otillräcklig utandning.">
				>
				["at0034"] = <
					text = <"Biots andning">
					description = <"Kluster av snabba och oregelbundna andetag som växlas med perioder av apné.">
				>
				["at0037"] = <
					text = <"Ansträngning">
					description = <"Nivå av ansträngning vid eller strax före observationen görs. Endast avsedd för att spela in ansträngning eftersom det kan påverka andning och där det normalt inte skulle registreras som en del av en generell klinisk observation.">
				>
				["at0044"] = <
					text = <"Apné">
					description = <"Andningen har upphört.">
				>
				["at0054"] = <
					text = <"Långvarig utandningsfas">
					description = <"Utandningsfasen är längre än normalt / vanligt. Associerad med obstruktiv luftvägssjukdom som astma.">
				>
				["at0055"] = <
					text = <"Ambient syre">
					description = <"Uppgifter om mängden syre som levereras till patienten vid tidpunkten för observation. Antagna värden på 21% syrekoncentration , Fi02 av 0,21 och syrgasströmningshastighet av 0 l / min eller 0 ml / min.">
				>
				["at0056"] = <
					text = <"Andra påverkande faktorer">
					description = <"Kommentar om tillfälliga påverkande faktorer som kan bidra till andetagets observation. Till exempel nivå av ångest, smärta, födointag hos spädbarn, trakeostomi.">
				>
			>
		>
	>
